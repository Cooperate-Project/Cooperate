grammar de.cooperateproject.modeling.textual.cls.Cls with org.eclipse.xtext.common.Terminals

import "http://www.cooperateproject.de/modeling/textual/cls/Cls"
import "http://www.eclipse.org/uml2/5.0.0/UML" as uml
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// ------------------------------------------------------------------------------------------
// ---------------------------------Root-----------------------------------------------------
// ------------------------------------------------------------------------------------------
ClassDiagram:
	{ClassDiagram}
	'@startclass' name=STRING
	(packageImports+=PackageImport)*
	classifiers+=Classifier*
	connectors+=Connector*
    '@endclass'
;

PackageImport:
	"import" package=[uml::Package|FQN]
;

FQN returns ecore::EString:
    ID ("." ID)*
;

// ------------------------------------------------------------------------------------------
// ---------------------------------Class Diagram--------------------------------------------
// ------------------------------------------------------------------------------------------

Classifier:
	Class | Interface
;

Class:
	visibility=Visibility? (abstract ?= 'abstract')? 'class' (longName = (STRING | ID) 'as')? referencedElement = [uml::Class|ID] '{' (members += Member)* '}'
;

Interface:
	visibility=Visibility? 'interface' (longName = (STRING | ID) 'as')? referencedElement = [uml::Interface|ID] '{' (members += Member)* '}'
;

TypeReference: 
	DataTypeReference | UMLTypeReference
;

DataTypeReference: 
	type = PrimitiveType
;

UMLTypeReference: 
	type = [uml::Type]
;


Member: 
	Attribute 
	| Method
;

Attribute:
	{Attribute} 
	(visibility = Visibility)? 
	(static ?= 'static')? 
	(final ?= 'final')? 
	referencedElement = [uml::Property|ID] ':' type = TypeReference
;
	
Method: 
	(visibility = Visibility)? 
	(abstract ?= 'abstract')? 
	(static ?= 'static')? 
	(final ?= 'final')? 
	referencedElement = [uml::Operation|ID] '(' 
	(parameters += Parameter (',' parameters += Parameter)*)* ')' 
	(':' type = TypeReference)?
;

Parameter:
	(visibility = Visibility)? 
	(static ?= 'static')? 
	(final ?= 'final')? 
	referencedElement = [uml::Parameter|ID] ':' type = TypeReference
;



Connector: 
	Generalization 
	| Association
	| Implementation
	| CommentLink
;

ClassifierAssociationEnd:
	UMLTypeReference
;

Generalization: 
	left = ClassifierAssociationEnd 'isa' right = ClassifierAssociationEnd
;

Implementation:
	left = ClassifierAssociationEnd 'impl' right = ClassifierAssociationEnd
;

CommentLink: 
	left = ClassifierAssociationEnd '-' 'note' comment = [uml::Comment|STRING]
;

Association:
	aggregationKind=AggregationKind  left = ClassifierAssociationEnd referencedElement = [uml::Association] right = ClassifierAssociationEnd
	(properties = AssociationProperties)? ('note' comment = [uml::Comment|STRING])?
;

AssociationProperties: 
	'[' cardinalityLeft = Cardinality ('|' cardinalityRight = Cardinality ('|' readingDirection = ReadingDirection)?)? ']'
;

Cardinality: 
	lowerBound=CardinalityBound ('..' upperBound=CardinalityBound)?
;

CardinalityBound returns ecore::EInt:
	('*') | INT
;

enum Visibility:
	PACKAGE = '~' 
	| PRIVATE = '-' 
	| PROTECTED = '#' 
	| PUBLIC = '+'
;

enum ReadingDirection:
	LEFT = '<'
	| RIGHT = '>'
;

enum PrimitiveType:
	STRING = 'string'
	| INT = 'int'
	| DOUBLE = 'double'
	| BOOLEAN = 'boolean'
	| CHAR = 'char'
	| BYTE = 'byte'
	| SHORT = 'short'
	| LONG = 'long'
	| FLOAT = 'float'
;

enum AggregationKind:
	NONE = 'asc'
	| AGGREGATION = 'agg'
	| COMPOSITION = 'com'
;
