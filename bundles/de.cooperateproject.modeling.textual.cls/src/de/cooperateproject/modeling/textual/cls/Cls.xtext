grammar de.cooperateproject.modeling.textual.cls.Cls with org.eclipse.xtext.common.Terminals

import "http://www.cooperateproject.de/modeling/textual/cls/Cls"
import "http://www.eclipse.org/uml2/5.0.0/UML" as uml
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// ------------------------------------------------------------------------------------------
// ---------------------------------Root-----------------------------------------------------
// ------------------------------------------------------------------------------------------
ClassDiagram:
	{ClassDiagram}
	'@startclass'
	(packageImports+=PackageImport)*
	classifiers+=Classifier*
	connectors+=Connector*
    '@endclass'
;

PackageImport:
	"import" package=[uml::Package|FQN]
;

FQN returns ecore::EString:
    ID ("." ID)*
;

// ------------------------------------------------------------------------------------------
// ---------------------------------Class Diagram--------------------------------------------
// ------------------------------------------------------------------------------------------

Classifier:
	Class | Interface
;

Class:
	visibility=Visibility? (abstract ?= 'abstract')? 'class' (longName = (STRING | ID) 'as')? referencedElement = [uml::Class|ID] '{' (members += Member)* '}'
;

Interface:
	visibility=Visibility? 'interface' (longName = (STRING | ID) 'as')? referencedElement = [uml::Interface|ID] '{' (members += Member)* '}'
;

TypeReference: 
	ClassifierReference | DataTypeReference | UMLTypeReference
;

ClassifierReference: 
	type = [Classifier]
;

DataTypeReference: 
	type = PrimitiveType
;

UMLTypeReference: 
	type = [uml::Type]
;


Member: 
	Attribute 
	| Method
;

Attribute:
	{Attribute} 
	(visibility = Visibility)? 
	(static ?= 'static')? 
	(final ?= 'final')? 
	referencedElement = [uml::Property|ID] ':' type = TypeReference
;
	
Method: 
	(visibility = Visibility)? 
	(abstract ?= 'abstract')? 
	(static ?= 'static')? 
	(final ?= 'final')? 
	referencedElement = [uml::Operation|ID] '(' 
	(parameters += Parameter (',' parameters += Parameter)*)* ')' 
	(':' type = TypeReference)?
;

Parameter:
	(visibility = Visibility)? 
	(static ?= 'static')? 
	(final ?= 'final')? 
	referencedElement = [uml::Parameter|ID] ':' type = TypeReference
;



Connector: 
	Generalization 
	| Association
	| Implementation
	| CommentLink
;

ClassifierAssociationEnd:
	ClassifierReference
;

Generalization: 
	left = ClassifierAssociationEnd 'isa' right = ClassifierAssociationEnd
;

Implementation:
	left = ClassifierAssociationEnd 'impl' right = ClassifierAssociationEnd
;

CommentLink: 
	left = ClassifierAssociationEnd '-' comment = Comment
;

Association: 
	left = ClassifierAssociationEnd '-'  
	right = ClassifierAssociationEnd (properties = AssociationProperties)? (comment = Comment)?
;


AssociationProperties: 
	'[' cardinalityLeft = Cardinality ('|' cardinalityRight = Cardinality ('|' referencedElement = [uml::Association] (readingDirection = ReadingDirection)?)?)? ']'
;

Cardinality: 
	lowerBound=CardinalityBound ('..' upperBound=CardinalityBound)?
;

CardinalityBound returns ecore::EInt:
	('*') | INT
;

Comment: 
	'note[' description = STRING ']'
;

enum Visibility:
	PACKAGE = '~' 
	| PRIVATE = '-' 
	| PROTECTED = '#' 
	| PUBLIC = '+'
;

enum ReadingDirection:
	LEFT = '<'
	| RIGHT = '>'
;

enum PrimitiveType:
	STRING = 'string'
	| INT = 'int'
	| DOUBLE = 'double'
	| BOOLEAN = 'boolean'
	| CHAR = 'char'
	| BYTE = 'byte'
	| SHORT = 'short'
	| LONG = 'long'
	| FLOAT = 'float'
;