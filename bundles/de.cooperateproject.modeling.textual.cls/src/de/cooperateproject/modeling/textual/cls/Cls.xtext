grammar de.cooperateproject.modeling.textual.cls.Cls with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate cls "http://www.cooperateproject.de/modeling/textual/cls/Cls"

// ------------------------------------------------------------------------------------------
// ---------------------------------Root-----------------------------------------------------
// ------------------------------------------------------------------------------------------
ClassDiagram:
	{ClassDiagram}
	'@startclass'
    (elements += ClassElement)*
    '@endclass'
;


// ------------------------------------------------------------------------------------------
// ---------------------------------Class Diagram--------------------------------------------
// ------------------------------------------------------------------------------------------


ClassElement: 
	ClassDef 
	| Class  
	| Connector
;

Type: 
	ClassType | DataType
;

ClassType: 
	type = [ClassName]
;

DataType: 
	type = DataTypeEnum
;

ClassDef: 
	('class' | 'interface') 
	'{' classes += ClassName (',' classes += ClassName)* '}'
;

ClassName: 
	Name
;

Name returns ClassName: 
	(name = ID )
	| ( longname = (STRING | ID) 'as' name = ID)
;

Class: 
	(abstract ?= 'abstract')?
	name = [ClassName]
	'{' (members += Member)* '}'
;

Member: 
	Attribute 
	| Methode
;

Attribute:
	{Attribute} 
	(visibility = Visibility)? 
	(static ?= 'static')? 
	(final ?= 'final')? 
	name=ID ':' type = Type
;
	
Methode: 
	(visibility = Visibility)? 
	(abstract ?= 'abstract')? 
	(static ?= 'static')? 
	(final ?= 'final')? 
	name = ID '(' 
	(attributes += Attribute (',' attributes += Attribute)*)* ')' 
	(':' type = Type)?
;

Connector: 
	Generalization 
	| Association
	| Implementation
	| CommentLink
;

Generalization: 
	left = AssociationEnd 'isa' right = AssociationEnd
;

Implementation:
	left = AssociationEnd 'impl' right = AssociationEnd
;

CommentLink: 
	left = AssociationEnd '-' comment = Comment
;

Association: 
	left = AssociationEnd '-'  
	right = AssociationEnd (cardinality = ConnectorCardinalitiy)? (comment = Comment)?
;

AssociationEnd: 
	type = [ClassName]
;

ConnectorLabel: 
	label = (ID | STRING)
;

ConnectorCardinalitiy: 
	{ConnectorCardinalitiy}
	'[' left = Cardinality ('|' right = Cardinality ('|' label = ConnectorLabel (direction = ('<' | '>'))?)?)? ']'
;

Cardinality: 
	('*') 
	| (INT '..' (('*') | INT))
;

Comment: 
	{Comment}
	'note[' comment = STRING ']'
;

enum Visibility:
	DEFAULT = '~' 
	| PRIVATE = '-' 
	| PROTECTED = '#' 
	| PUBLIC = '+'
;


enum DataTypeEnum:
	STRING = 'string'
	| INT = 'int'
	| DOUBLE = 'double'
	| BOOLEAN = 'boolean'
	| CHAR = 'char'
	| BYTE = 'byte'
	| SHORT = 'short'
	| LONG = 'long'
	| FLOAT = 'float'
;