transformation Textual_to_Graphical_for_Class(in textual : TXT, out graphical : NOTATION, in umlmodel : UML, in umlprimitives : UML);

modeltype NOTATION uses notation('http://www.eclipse.org/gmf/runtime/1.0.2/notation');
modeltype UML uses uml('http://www.eclipse.org/uml2/5.0.0/UML');
modeltype TXT uses cls('http://www.cooperateproject.de/modeling/textual/cls/Cls');
modeltype ECORE uses ecore('http://www.eclipse.org/emf/2002/Ecore');
modeltype STYLE uses style('http://www.eclipse.org/papyrus/infra/viewpoints/policy/style');

main() {
	var diagrams := textual.rootObjects()[cls::ClassDiagram];
	assert fatal (diagrams->size() = 1);
	diagrams->map diagramToDiagram();
}

mapping cls::ClassDiagram::diagramToDiagram() : notation::Diagram {
	type := "PapyrusUMLClassDiagram";
	name := self.name;
	measurementUnit := notation::MeasurementUnit::Pixel;
	
	var umlRootElement := self.packageImports->first().package;
	element := umlRootElement.toEObject();
	
	// package imports?
	children += self.classifiers->map classifierToShape();
	children += self.connectors->selectByKind(cls::MultiAssociation)->map multiAssociationToShape();
	children += self.connectors->select(isCommented())->selectByKind(cls::Commentable)->map commentLinkToShape();
	edges += self.connectors->select(not oclIsKindOf(cls::CommentLink))->map connectorToConnector(self.classifiers);
	edges += self.connectors->selectByKind(cls::MultiAssociation).connectorEnds->map connectorEndToEdge();
	edges += self.connectors->select(isCommented())->selectByKind(cls::Commentable)->map commentableToEdge(self.classifiers);
	
	styles += self.map stringValueStyle();
	styles += self.map diagramStyle();
	styles += self.map papyrusViewStyle(umlRootElement);
}

// classifiers

mapping cls::Classifier::classifierToShape() : notation::Shape
merges cls::Classifier::classToShape, cls::Classifier::interfaceToShape
{
	element := self.referencedElement.oclAsType(ecore::EObject);
	
	children += self.map classifierToUnknownDecorationNode();
	children += self.map classifierToFloatingLabelDecorationNode();
	
	children += self.map classifierToAttributesCompartment();
	children += self.map classifierToOperationsCompartment();
	children += self.map classifierToSignalsCompartment();
	
	layoutConstraint := result.toEObject().map notationBounds();
}

mapping cls::Classifier::interfaceToShape() : notation::Shape
when {self.oclIsKindOf(cls::Interface)}
{
	type := "2004";
}

mapping cls::Classifier::classToShape() : notation::Shape
when {self.oclIsKindOf(cls::Class)}
{
	type := "2008";
}

mapping cls::Commentable::commentLinkToShape() : notation::Shape
{
	type := "2012";
	element := self.comment.toEObject();
	children += self.map commentLabelBodyHintDecoration();
	layoutConstraint := result.toEObject().map notationBounds();
}

mapping cls::MultiAssociation::multiAssociationToShape() : notation::Shape
{
	type := "2015";
	element := self.referencedElement.toEObject();
	layoutConstraint := result.toEObject().map locationConstraint();
	children += self.map multiAssociationToFloatingLabelDecorationNode();
}

mapping cls::Classifier::classifierToAttributesCompartment() : notation::BasicCompartment
merges cls::Classifier::classToAttributesCompartment, cls::Classifier::interfaceToAttributesCompartment
{
	children += self.members->select(oclIsKindOf(cls::Attribute))->oclAsType(cls::Attribute)->map attributeToShape();
	styles += result.toEObject().map titleStyle();
	styles += result.toEObject().map sortingStyle();
	styles += result.toEObject().map filteringStyle();
	layoutConstraint := result.toEObject().map notationBounds();
}

mapping cls::Classifier::classToAttributesCompartment() : notation::BasicCompartment
when {self.oclIsKindOf(cls::Class)}
{
	type := "7017";
}

mapping cls::Classifier::interfaceToAttributesCompartment() : notation::BasicCompartment
when {self.oclIsKindOf(cls::Interface)}
{
	type := "7006";
}

mapping cls::Attribute::attributeToShape() : notation::Shape {
	type := "3012";
	element := self.referencedElement.oclAsType(ecore::EObject);
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::Classifier::classifierToOperationsCompartment() : notation::BasicCompartment
merges cls::Classifier::classToOperationsCompartment, cls::Classifier::interfaceToOperationsCompartment
{
	children += self.members->select(oclIsKindOf(cls::Method))->oclAsType(cls::Method)->map methodToShape();
	styles += result.toEObject().map titleStyle();
	styles += result.toEObject().map sortingStyle();
	styles += result.toEObject().map filteringStyle();
	layoutConstraint := result.toEObject().map notationBounds();
}

mapping cls::Classifier::classToOperationsCompartment() : notation::BasicCompartment
when {self.oclIsKindOf(cls::Class)}
{
	type := "7018";
}

mapping cls::Classifier::interfaceToOperationsCompartment() : notation::BasicCompartment
when {self.oclIsKindOf(cls::Interface)}
{
	type := "7007";
}

mapping cls::Method::methodToShape() : notation::Shape
merges cls::Method::classMethodToShape, cls::Method::interfaceMethodToShape
{
	element := self.referencedElement.oclAsType(ecore::EObject);
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::Method::classMethodToShape() : notation::Shape
when {self.owner.oclIsKindOf(cls::Class)}
{
	type := "3013";
}

mapping cls::Method::interfaceMethodToShape() : notation::Shape
when {self.owner.oclIsKindOf(cls::Interface)}
{
	type := "3007";
}

mapping cls::Classifier::classifierToSignalsCompartment() : notation::BasicCompartment
merges cls::Classifier::classToSignalsCompartment, cls::Classifier::interfaceToSignalsCompartment
{
	styles += result.toEObject().map titleStyle();
	styles += result.toEObject().map sortingStyle();
	styles += result.toEObject().map filteringStyle();
	layoutConstraint := result.toEObject().map notationBounds();
}

mapping cls::Classifier::classToSignalsCompartment() : notation::BasicCompartment
when {self.oclIsKindOf(cls::Class)}
{
	type := "7019";
}

mapping cls::Classifier::interfaceToSignalsCompartment() : notation::BasicCompartment
when {self.oclIsKindOf(cls::Interface)}
{
	type := "7008";
}



mapping cls::Classifier::classifierToUnknownDecorationNode() : notation::DecorationNode
merges cls::Classifier::classToUnknownDecorationNode, cls::Classifier::interfaceToUnknownDecorationNode
{
}

mapping cls::Classifier::classToUnknownDecorationNode() : notation::DecorationNode
when{self.oclIsKindOf(cls::Class)}
{
	type := "5029";
}

mapping cls::Classifier::interfaceToUnknownDecorationNode() : notation::DecorationNode
when{self.oclIsKindOf(cls::Interface)}
{
	type := "5011";
}

mapping cls::Classifier::classifierToFloatingLabelDecorationNode() : notation::DecorationNode
merges cls::Classifier::classToFloatingLabelDecorationNode, cls::Classifier::interfaceToFloatingLabelDecorationNode
{
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::Classifier::classToFloatingLabelDecorationNode() : notation::DecorationNode
when {self.oclIsKindOf(cls::Class)}
{
	type := "8510";
}

mapping cls::Classifier::interfaceToFloatingLabelDecorationNode() : notation::DecorationNode
when {self.oclIsKindOf(cls::Interface)}
{
	type := "8507";
}


// connectors

mapping cls::Connector::connectorToConnector(classifiers : OrderedSet(cls::Classifier)) : notation::Connector
disjuncts cls::Association::connectorToEdge, cls::Generalization::generalizationToEdge, cls::Implementation::implementationToEdge
{
}

mapping cls::Association::connectorToEdge(classifiers : OrderedSet(cls::Classifier)) : notation::Connector
when { self.oclIsKindOf(cls::Association) }
{
	type := "4001";
	
	source := self.left.oclAsType(UMLTypeReference).type.typeToView();
	target := self.right.oclAsType(UMLTypeReference).type.typeToView();
	element := self.referencedElement.toEObject();

	children += self.map stereotypeDecorationAssocation();
	children += self.map nameDecorationAssociation();
	children += self.properties.map targetRoleDecoration();
	children += self.properties.map sourceRoleDecoration();
	children += self.properties.map sourceMultiplicityDecoration();
	children += self.properties.map targetMultiplicityDecoration();
	
	bendpoints := result.map connectorToBendpoint();
	styles += result.toEObject().map fontStyle();
}

mapping cls::Generalization::generalizationToEdge(classifiers : OrderedSet(cls::Classifier)) : notation::Connector
when { self.oclIsKindOf(cls::Generalization) }
{
	type := "4002";
	
	var specialClassifier := self.left.oclAsType(UMLTypeReference).type.oclAsType(uml::Classifier);
	var generalClassifier := self.right.oclAsType(UMLTypeReference).type.oclAsType(uml::Classifier);
	
	source := specialClassifier.typeToView();
	target := generalClassifier.typeToView();
	element := specialClassifier.generalization->any(specific = specialClassifier).toEObject();
	
	children += self.map stereotypeDecorationGeneralization();
	
	bendpoints := result.map connectorToBendpoint();
	styles += result.toEObject().map fontStyle();
}

mapping cls::Implementation::implementationToEdge(classifiers : OrderedSet(cls::Classifier)) : notation::Connector
when { self.oclIsKindOf(cls::Implementation) }
{
	type := "4003";
	
	var implementingClassifier := self.left.oclAsType(UMLTypeReference).type.oclAsType(uml::BehavioredClassifier);
	var implementedInterface := self.right.oclAsType(UMLTypeReference).type.oclAsType(uml::Classifier);
	
	source := implementingClassifier.typeToView();
	target := implementedInterface.typeToView();
	
	element := implementingClassifier.interfaceRealization->any(contract = implementedInterface).toEObject();
	
	children += self.map stereotypeDecorationImplementation();
	children += self.map nameDecorationImplementation();
	
	bendpoints := result.map connectorToBendpoint();
	styles += result.toEObject().map fontStyle();
}

mapping cls::MemberEnd::connectorEndToEdge() : notation::Connector
{
	type := "4019";
	
	var umlAssociation := self.association.referencedElement;
	element := umlAssociation.toEObject();

	var associationDiamondShape := resolveoneIn(cls::MultiAssociation::multiAssociationToShape, s : notation::Shape | s.element = umlAssociation.toEObject());
	assert error (associationDiamondShape != null) with log('Could not find the association diamond for a n-ary association.', umlAssociation);
	source := associationDiamondShape;
	
	var referencedNotationElement := self.type.type.typeToView();
	assert error (referencedNotationElement != null) with log('Could not find the referenced view for a UML type.', self.type);
	target := referencedNotationElement;

	children += self.map targetRoleDecorationMemberEnd();
	children += self.map targetMultiplicityDecorationMemberEnd();

	bendpoints := result.map connectorToBendpoint();
	styles += result.toEObject().map fontStyle();
	styles += self.map semanticBranchStyle();
}

mapping cls::Commentable::commentableToEdge(classifiers : OrderedSet(cls::Classifier)) : notation::Connector
{
	type := "4013";
	
	var commentShape := self.resolveoneIn(cls::Commentable::commentLinkToShape);
	var classifier := self.getCommentedElement().oclAsType(uml::Classifier);
	
	source := commentShape;
	target := classifier.typeToView();
	element := null;
	
	bendpoints := result.map connectorToBendpoint();
	styles += result.toEObject().map fontStyle();
}

mapping notation::Connector::connectorToBendpoint() : notation::RelativeBendpoints
{
}


// decoration nodes

/*
5038 Comment Label Body Hint
6001 Stereotype (Association)
6002 Name (Association)
6003 TargetRole (Association)
6005 SourceRole (Association)
6007 Stereotype (Generalization)
6008 Stereotype (Interface Realization)
6009 Name (Interface Realization)
6024 TargetRole (AssociationBranch)
6033 SourceMultiplicity
6034 TargetMultiplicity
6035 TargetMultiplicity (AssociationBranch)
8521 Floating Label (Association Diamond)
*/

mapping cls::Commentable::commentLabelBodyHintDecoration() : notation::DecorationNode
{
	type := "5038";
}

mapping cls::Association::stereotypeDecorationAssocation() : notation::DecorationNode
{
	type := "6001";
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::Association::nameDecorationAssociation() : notation::DecorationNode
{
	type := "6002";
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::AssociationProperties::targetRoleDecoration() : notation::DecorationNode
when {self.propertyRight != null}
{
	type := "6003";
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::AssociationProperties::sourceRoleDecoration() : notation::DecorationNode
when {self.propertyLeft != null}
{
	type := "6005";
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::Generalization::stereotypeDecorationGeneralization() : notation::DecorationNode
{
	type := "6007";
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::Implementation::stereotypeDecorationImplementation() : notation::DecorationNode
{
	type := "6008";
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::Implementation::nameDecorationImplementation() : notation::DecorationNode
{
	type := "6009";
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::MemberEnd::targetRoleDecorationMemberEnd() : notation::DecorationNode
{
	type := "6024";
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::AssociationProperties::sourceMultiplicityDecoration() : notation::DecorationNode
when {self.cardinalityLeft != null}
{
	type := "6033";
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::AssociationProperties::targetMultiplicityDecoration() : notation::DecorationNode
when {self.cardinalityRight != null}
{
	type := "6034";
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::MemberEnd::targetMultiplicityDecorationMemberEnd() : notation::DecorationNode
when {self.cardinality != null}
{
	type := "6035";
	layoutConstraint := result.toEObject().map locationConstraint();
}

mapping cls::MultiAssociation::multiAssociationToFloatingLabelDecorationNode(): notation::DecorationNode
{
	type := "8521";
	layoutConstraint := result.toEObject().map locationConstraint();
}

// style stuff

mapping ecore::EObject::locationConstraint() : notation::Location
{
}

mapping ecore::EObject::titleStyle() : notation::TitleStyle
{
}

mapping ecore::EObject::sortingStyle() : notation::SortingStyle
{
}

mapping ecore::EObject::filteringStyle() : notation::FilteringStyle
{
}

mapping ecore::EObject::notationBounds() : notation::Bounds
{
}

mapping ecore::EObject::fontStyle() : notation::FontStyle
{
}

mapping cls::ClassDiagram::stringValueStyle() : notation::StringValueStyle
{
	name := "diagram_compatibility_version";
	stringValue := "1.1.0";	
}

mapping cls::ClassDiagram::diagramStyle() : notation::DiagramStyle
{	
}

mapping cls::ClassDiagram::papyrusViewStyle(rootElement : uml::Package) : style::PapyrusViewStyle
{
	owner := rootElement.toEObject();
}

mapping cls::MemberEnd::semanticBranchStyle() : notation::EObjectValueStyle
{
	name := "Semantic_Branch_Style";
	eObjectValue := self.determineProperty().toEObject();
}

query cls::MemberEnd::determineProperty() : uml::Property
{
	var umlAssociation := self.association.referencedElement.oclAsType(uml::Association);
	var referencedProperty := self.referencedElement;
	
	var candidates := umlAssociation.memberEnd->select(p | p.matches(self));
	return if candidates->size() = 1 then candidates->any(true) else referencedProperty.oclAsType(uml::Property) endif;
}

query uml::Property::matches(memberEnd : cls::MemberEnd) : Boolean
{
	return
		memberEnd.referencedElement = self or
		(
			self.type = memberEnd.type.type and
			(
				memberEnd.cardinality = null or
				self.matches(memberEnd.cardinality)
			)
		);
}

query uml::MultiplicityElement::matches(cardinality : cls::Cardinality) : Boolean
{
	return
		self.lower = cardinality.lowerBound and
		(
			self.upper = cardinality.upperBound or
			(cardinality.upperBound = -1 and self.upper = *) 
		);
}

// helpers

query OclAny::toEObject() : ecore::EObject {
	return self.oclAsType(ecore::EObject);
}

query uml::Type::typeToView() : notation::View
{
	return if self.oclIsKindOf(uml::Association) then self.typeToEdge() else self.typeToShape() endif;
}

query uml::Type::typeToShape() : notation::Shape
{
	return resolveoneIn(cls::Classifier::classifierToShape, s : notation::Shape | s.element = self.toEObject());
}

query uml::Type::typeToEdge() : notation::Edge
{
	return resolveoneIn(cls::Connector::connectorToConnector, e : notation::Edge | e.element = self.toEObject());
}

query cls::Connector::isCommentable() : Boolean
{
	return self.oclIsKindOf(cls::Commentable);
}

query cls::Connector::isCommented() : Boolean
{
	return self.isCommentable() and self.oclAsType(cls::Commentable).comment != null;
}