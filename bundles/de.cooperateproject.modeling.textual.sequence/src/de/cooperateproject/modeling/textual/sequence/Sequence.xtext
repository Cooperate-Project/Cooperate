grammar de.cooperateproject.modeling.textual.sequence.Sequence with org.eclipse.xtext.common.Terminals

import "platform:/plugin/org.eclipse.uml2.uml/model/UML.ecore" as uml
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate sequence "http://www.cooperateproject.de/modeling/textual/sequence/Sequence"


SequenceDiagram:
    {SequenceDiagram}
    '@start-seqd' title=STRING
    rootPackage=RootPackage
    '@end-seqd'
;

RootPackage:
    'rootElement' name = FQN
    actors += Actor* 
    interactionFragments += InteractionFragment*
;

Actor:
    'act' (deferred?='deferred')? 
        ((name = ID ':' type=[uml::Classifier|FQN])
        | (':' type=[uml::Classifier|FQN] 'as' alias = ID)
        | (name = STRING ':' type=[uml::Classifier|FQN] 'as' alias = ID))
    ('role['actorType=ActorType']')? //comments+=Comment? 
;

InteractionFragment: 
    Message | OccurenceSpecification | CombinedFragment
;

Message:
    (StandardMessage    |
        ResponseMessage | 
        FoundMessage    |
        LostMessage     |
        CreateMessage   |
        DestructionMessage) 
    ('num['order = Order']')?
    timeConstraint=TimeConstraint?
;

TimeConstraint:
	'time[' constraints+=InnerTimeConstraint+ ']'
;

InnerTimeConstraint:
    type=TimeConstraintType'='value = STRING
;

StandardMessage:
    type=MessageType name=STRING '(' left = [Actor|FQN] ',' right = [Actor|FQN] ')'
;

ResponseMessage:
   'resp' name=STRING '(' (left = [Actor|FQN]|'_') ',' (right = ([Actor|FQN])|'_') ')'   
;

FoundMessage:
    'found'? type=MessageType name=STRING '(' '_' ',' right = [Actor|FQN]')'
;

LostMessage:
    'lost'? type=MessageType name=STRING '(' left = [Actor|FQN] ',' '_'')'
;

CreateMessage:
    'create' name=STRING? '(' left = [Actor|FQN] ',' right = [Actor|FQN] ')'
;

DestructionMessage:
    'dest' name=STRING? '(' left = [Actor|FQN] ',' right = [Actor|FQN] ')'
;


OccurenceSpecification:
    DestructionOccurenceSpecification
;

DestructionOccurenceSpecification: 
    'dest' name=ID
;

CombinedFragment:
    Alternative | Option | Parallel | Critical | Loop
;

Alternative:
    'alt' '{'
        alternatives += ConditionedFragment+
    '}'
;

Option:
    'opt' '{'
        option += ConditionedFragment
    '}'
;

Parallel:
    'par' '{'
        parallels += ParallelFragment+
    '}'
;

Critical:
    'critical' '{'
        fragments += InteractionFragment+
    '}'
;

Loop:
    'loop''['condition=STRING']''{'
        fragments += InteractionFragment+
    '}'
;

ParallelFragment:
    (('{' fragments += InteractionFragment* '}') | fragments += InteractionFragment)
;

ConditionedFragment:
    '['condition=STRING']' (('{' fragments += InteractionFragment* '}') | fragments += InteractionFragment)  
;

FQN:
    ID ('.' ID)*
;

Comment:
    'note''['body = CommentBody']'
;

Order:
    INT ('.' INT)*
; 

CommentBody returns ecore::EString:
    STRING
;

enum MessageType:
    SYNC = "sync"
    | ASYNC = "async"
;

enum ActorType:
    HUMAN = 'human'
    | MACHINE = 'machine'
;

enum TimeConstraintType:
    DURATION = 'd'
    | TIME = 't'
    | MAXIMUM = 'max'
    | DIFFERENCE = 'diff'
;

